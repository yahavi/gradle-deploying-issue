/*
 * Copyright (C) 2020 JFrog Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
//        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.13.0') // This works
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.14.1') // This doesn't work
    }
}

def javaProjects() {
    subprojects.findAll { new File(it.projectDir, 'src').directory }
}

allprojects {
    apply plugin: 'com.jfrog.artifactory'
    group = 'org.jfrog.test.gradle.publish' // <-- Removing the group will help
    version = "1.0"
    repositories {
        jcenter()
    }
}

configure(javaProjects()) {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    compileJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

artifactoryPublish.skip = true

artifactory {
    contextUrl = "${artifactory_url}"
    publish {
        repository {
            repoKey = 'gradle-release-local' // The Artifactory repository key to publish to
            username = "${artifactory_user}" // The publisher user name
            password = "${artifactory_password}" // The publisher password
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishPom = false // Publish generated POM files to Artifactory (true by default)
            publishIvy = false // Publish generated Ivy descriptor files to Artifactory (true by default)
        }
    }
}
